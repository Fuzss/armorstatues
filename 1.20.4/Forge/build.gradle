apply from: 'https://raw.githubusercontent.com/Fuzss/modresources/main/gradle/forge.gradle'

def versionCatalog = extensions.getByType(VersionCatalogsExtension).named("libs")

dependencies {
    // Puzzles Lib
    api fg.deobf(libs.puzzleslib.forge.get())

    // Puzzles Api
    api(fg.deobf(libs.puzzlesapi.forge.get())) {
        transitive = false
    }
    jarJar(fg.deobf(libs.puzzlesapi.forge.get()) {
        jarJar.ranged(it, "[${libs.versions.puzzlesapi.get()},)")
        transitive = false
    })

    // Quality of Life Mods
    versionCatalog.findLibrary("bettermodsbutton.forge").ifPresent {
        runtimeOnly fg.deobf(it.get())
    }
//    runtimeOnly fg.deobf("fuzs.bettermodsbutton:bettermodsbutton-forge:${libs.versions.bettermodsbutton.get()}")
    versionCatalog.findLibrary("forgeconfigscreens.forge").ifPresent {
        runtimeOnly fg.deobf(it.get())
    }
}

task signJar(type: net.minecraftforge.gradle.common.tasks.SignJar, dependsOn: tasks.reobfJarJar) {
    onlyIf { project.hasProperty('keyStore') }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = outputFile = tasks.jarJar.archivePath
}

jar.finalizedBy 'signJar'
signJar.mustRunAfter 'reobfJar'
